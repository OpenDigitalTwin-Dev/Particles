/*!
 * \file   ImplicitNorton3.mfront
 * \brief  
 * 
 * \author Helfer Thomas
 * \date   1 07 2014
 */

@DSL Implicit;
@Behaviour ImplicitNorton3;
@Description{
  This file implements the Norton law, described as:
  "$$"
  "\left\{"
  "  \begin{aligned}"
  "    \tepsilonto   &= \tepsilonel+\tepsilonvis \\"
  "    \tsigma       &= \tenseurq{D}\,:\,\tepsilonel\\"
  "    \tdepsilonvis &= \dot{p}\,\tenseur{n} \\"
  "    \dot{p}       &= A\,\sigmaeq^{m}"
  "  \end{aligned}"
  "\right."
  "$$"
  We test here the substepping facility provided by the UMAT Interface.
}
@ModellingHypotheses {".+"};

@Brick StandardElasticity;

@Epsilon 1.e-15;
@UMATUseTimeSubStepping[umat] true;
@UMATMaximumSubStepping[umat] 20;
@CyranoUseTimeSubStepping[cyrano] true;
@CyranoMaximumSubStepping[cyrano] 20;

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@LocalVariable stress lambda;
@LocalVariable stress mu;

@StateVariable strain p;
p.setGlossaryName("EquivalentViscoplasticStrain");
@PhysicalBounds p in [0:*[;

/* Initialize Lame coefficients */
@InitLocalVariables{
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
  if(dt>0.1){
    throw(DivergenceException());
  }
} // end of @InitLocalVars

@Integrator{
  const real A = 8.e-67;
  const real E = 8.2;
  const auto seq = sigmaeq(sig);
  const auto tmp = A*pow(seq,E-1.);
  const auto df_dseq = E*tmp;
  const auto iseq = 1/(max(seq,real(1.e-12)*young));
  const Stensor n = 3*deviator(sig)*(iseq/2);
  feel += dp*n;
  fp   -= tmp*seq*dt;
  // jacobian
  dfeel_ddeel += 2*mu*theta*dp*iseq*(Stensor4::M()-(n^n));
  dfeel_ddp    = n;
  dfp_ddeel    = -2*mu*theta*df_dseq*dt*n;
} // end of @Integrator
