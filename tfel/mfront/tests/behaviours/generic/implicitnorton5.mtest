@Author Thomas Helfer;
@Date 28/07/2018;

@PredictionPolicy 'LinearPrediction';
@XMLOutputFile @xml_output@;
@MaximumNumberOfSubSteps 1;
@Behaviour<generic> @library@ 'ImplicitNorton5';

@MaterialProperty<constant> 'YoungModulus'     150.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@Real 'sxx' 20e6;
@ImposedThermodynamicForce 'StressXX' 'sxx';
// Initial value of the elastic strain
@Real 'EELXX0' 0.00013333333333333333;
@Real 'EELZZ0' -0.00004;
@InternalStateVariable 'ElasticStrain' {'EELXX0','EELZZ0','EELZZ0',0.,0.,0.};
// Initial value of the total strain
@Gradient {'EELXX0', 'EELZZ0', 'EELZZ0', 0., 0., 0.};
// Initial value of the total stresses
@ThermodynamicForce {'sxx', 0., 0., 0., 0., 0.};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,3600 in 20};

// tests on strains
// note: EquivalentViscoplasticStrain is known at 1.e-12 (defaut value
// for @StrainEpsilon), thus we may expect the strain to be known at
// '3.6*1.e-9'. If pratice, things are a bit better but not much
// better.
@Real 'A' 8.e-67;
@Real 'E' 8.2;
@Test<function> 'StrainXX' 'EELXX0+A*sxx**E*t'     1.e-9;
@Test<function> 'StrainYY' 'EELZZ0-0.5*A*sxx**E*t' 1.e-10;
@Test<function> 'StrainZZ' 'EELZZ0-0.5*A*sxx**E*t' 1.e-10;
@Test<function> 'StrainXY' '0.'                    1.e-10;
// tests on internal state variables
@Test<function> 'ElasticStrainXX' 'EELXX0'  1.e-12;
@Test<function> 'ElasticStrainYY' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainZZ' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainXY' '0.'      1.e-12;
@Test<function> 'EquivalentViscoplasticStrain' 'A*sxx**E*t' 1.e-12;
// this test is a bit paranoiac since SXX is imposed
@Test<function> 'StressXX' 'sxx'     1.e-3;
// check that the mechanical equilibrium is satisfied
@Test<function> 'StressYY' '0.'      1.e-3;
@Test<function> 'StressZZ' '0.'      1.e-3;
@Test<function> 'StressXY' '0.'      1.e-3;
