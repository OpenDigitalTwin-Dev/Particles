@DSL Default{parameters_as_static_variables : true};
@Behaviour ElasticityParametersAsStaticVariables;
@Author Thomas Helfer;
@Date 23 / 11 / 06;

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@LocalVariable stress lambda, mu;

@InitLocalVariables {
  lambda = computeLambda(young, nu);
  mu = computeMu(young, nu);
}

@PredictionOperator {
  static_cast<void>(smt);  // remove unused variable warning
  Dt = lambda * Stensor4::IxI() + 2 * mu * Stensor4::Id();
}

@Integrator {
  constexpr auto id = StrainStensor::Id();
  const auto e = eto + deto;
  sig = lambda * trace(e) * id + 2 * mu * (e);
}

@TangentOperator {
  static_cast<void>(smt);  // remove unused variable warning
  Dt = lambda * Stensor4::IxI() + 2 * mu * Stensor4::Id();
}

// defining a source just to generate a source file. Otherwise, this would lead
// to an error because the `mfront_behaviour_check_library` function used by
// TFEL' cmake infrastructure does not support when no sources are generated
@Sources {
  [[maybe_unused]] static void f(){}
}
