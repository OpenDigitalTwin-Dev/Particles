@DSL Implicit;
@UseQt true;
@Author Thomas Helfer;
@Date 21 / 09 / 2008;
@Behaviour ImplicitNorton7;
@Description{};

@Brick "StandardElasticity";
@Epsilon 1.e-16;

@ElasticMaterialProperties{250e9, 0.3};

@StateVariable strain p;
p.setGlossaryName("EquivalentViscoplasticStrain");
@PhysicalBounds p in [0:*[;
			  
@Integrator{
  constexpr auto s0 = stress(100e6);
  constexpr auto de0 = strainrate(0.31848573644279365);
  constexpr auto E = real(8.2);
  const auto seps = real(1.e-12) * young;
  const auto seq = sigmaeq(sig);
  const auto rseq = seq / s0;
  const auto iseq = 1 / (std::max(seq, seps));
  const auto n = 3 * deviator(sig) * (iseq / 2);
  // elasticity
  feel += dp * n;
  // viscoplasticity
  const auto tmp = de0 * pow(rseq, E - 1.);
  fp -= tmp * rseq * dt;
  // jacobian
  dfeel_ddeel += 2 * mu * theta * dp * iseq * (Stensor4::M() - (n ^ n));
  dfeel_ddp = n;
  dfp_ddeel = -2 * theta * (mu / s0) * E * tmp * dt * n;
} // end of @Integrator
